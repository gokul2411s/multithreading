plugins {
    id 'java'
    id 'application'
}

group 'personal.gokul2411s'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'

    // https://mvnrepository.com/artifact/args4j/args4j-site
    compile group: 'args4j', name: 'args4j', version: '2.33'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

run {
    standardInput = System.in
}

// Creates scripts for entry points.
def createScript(project, mainClass, name) {
    project.tasks.create(name: name, type: CreateStartScripts) {
        outputDir = new File(project.buildDir, 'scripts')
        mainClassName = mainClass
        applicationName = name
        classpath = project.tasks[JavaPlugin.JAR_TASK_NAME].outputs.files + project.configurations.runtime
    }
    project.tasks[name].dependsOn(project.jar)

    project.applicationDistribution.with {
        into("bin") {
            from(project.tasks[name])
            fileMode = 0755
        }
    }
}

startScripts.enabled = false
run.enabled = false
createScript(project, 'personal.gokul2411s.multithreading.dependent_tasks.thread_per_task.Simulation', 'dependent-tasks-thread-per-task-simulation')
createScript(project, 'personal.gokul2411s.multithreading.dependent_tasks.fixed_number_of_threads.Simulation', 'dependent-tasks-fixed-number-of-threads-simulation')
